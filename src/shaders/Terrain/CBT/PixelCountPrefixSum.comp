#version 460

#ifdef GLSLANGVALIDATOR
    // otherwise glslangValidator complains about #include
    //for OpenGL they will be replaced when shader is loaded
    #extension GL_GOOGLE_include_directive : require

    #ifndef SHADING_GROUP_SIZE
        #define SHADING_GROUP_SIZE 0
    #endif
#endif

#extension GL_KHR_shader_subgroup_basic : require
#extension GL_KHR_shader_subgroup_arithmetic : require

layout(local_size_x = 7, local_size_y = 1, local_size_z = 1) in;

#include "indirectCommandStructs.glsl"
layout(std430, binding = 1) writeonly restrict buffer computeCommand
{
    DispatchIndirectCommand compCmd[8];
};

layout(std430, binding = 4) buffer pixelBuffer
{   
    uint perGroupStartIndex[7];
    uint perGroupAmount[7];
    uint pixels[];
};

void main()
{
    //we only start a single subgroup and use subgroup intrinsics to perform prefix sum
    //  possible because values to sum (just 7) is less than (at least NV and AMD) subgroup size 
    uint groupAmount = perGroupAmount[gl_SubgroupInvocationID];
    
    //0th dispatch command contains count for CBT update, so write from 1 onwards
    compCmd[1 + gl_SubgroupInvocationID].num_groups_x = ((groupAmount + SHADING_GROUP_SIZE - 1) / SHADING_GROUP_SIZE);

    uint exclusiveScan = subgroupExclusiveAdd(groupAmount);
    subgroupBarrier(); //Dont think this is even needed
    perGroupStartIndex[gl_SubgroupInvocationID] = exclusiveScan;
    #ifdef RESET_AMOUNT_COUNTER //needed when caching is not used
        perGroupAmount[gl_SubgroupInvocationID] = 0; //reset to 0 for default 2nd pass
    #endif
}